PREFIX spav: <http://vocab.performing-arts.ch/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

WITH <http://example.com/sts>

DELETE {
  ?s spav:r_hasProducer ?label .
}
INSERT { 
  ?s spav:hasProducer ?conceptUri .

  ?conceptUri a spav:E17_CorporateBody ;
    rdfs:label ?label ;
    skos:notation ?notation .
}
WHERE {
    
    ?s a <http://vocab.performing-arts.ch/E9_PerformingArtsProduction> ;
        spav:r_hasProducer ?label ;
        spav:spa_theatercode ?notation .
  
    # label is not clean right now so we do have to hash it for the moment. Will create duplicates with different writing
    BIND(URI(CONCAT("http://data.performing-arts.ch/corporatebody/", SHA1(?label))) AS ?conceptUri)

}