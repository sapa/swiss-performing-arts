PREFIX spav: <http://vocab.performing-arts.ch/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dct: <http://purl.org/dc/terms/>

WITH <http://example.com/sts>

DELETE {
  ?s ?spa_type ?concept .
}
INSERT { 
	?s ?relationUri ?conceptUri .
	
  	?conceptUri a skos:Concept ;
    	skos:prefLabel ?concept ;
    	skos:inScheme ?schemeUri .
 
    ?schemeUri a skos:ConceptScheme ;
    	dct:title ?scheme . 
}
WHERE {
    
  ?s a <http://vocab.performing-arts.ch/E9> .
  ?s ?spa_type ?concept .
  
  FILTER( ?spa_type IN( spav:r_isLocatedAt, spav:r_isSubjectOf1, spav:r_isSubjectOf2, spav:r_isSubjectOf3, spav:r_hasContributor1, spav:r_isAssociatedWith, spav:r_hasSetDesigner, spav:r_hasComposer, spav:r_hasChoreographer, spav:r_hasChoralConductor, spav:r_hasPerformer, spav:r_hasDramaturge, spav:r_hasCostumeDesigner, spav:r_hasLightingDesigner, spav:r_hasConductor, spav:r_hasPosterDesigner, spav:r_hasProjectionDesigner, spav:r_hasStageDirector, spav:r_hasAssistantStageDirector, spav:r_hasDate, spav:r_hasContributor2, spav:r_hasSoundDesigner, spav:r_hasTranslator, spav:r_hasVideoDesigner ))
  
  BIND(URI(CONCAT("http://data.performing-arts.ch/sts/concept/", SHA1(?concept))) AS ?conceptUri)
  BIND( STRAFTER( STR(?spa_type), "http://vocab.performing-arts.ch/r_") AS ?scheme)
  BIND( STRAFTER( STR(?spa_type), "http://vocab.performing-arts.ch/") AS ?schemeDirty)
  BIND(URI(CONCAT("http://data.performing-arts.ch/scheme/", ENCODE_FOR_URI(?schemeDirty))) AS ?schemeUri)
  BIND(URI(CONCAT("http://vocab.performing-arts.ch/", ENCODE_FOR_URI(?scheme))) AS ?relationUri)

}